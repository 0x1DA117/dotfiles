# -*- mode: shell-script -*-

#
# Completion
#

# Case-insensitive matching
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}'

# Select completions with arrow keys
zstyle ':completion:*' menu select

# Group results by category
zstyle ':completion:*' group-name ''

setopt always_to_end
setopt auto_menu
setopt complete_in_word

autoload -Uz compinit
compinit

#
# Plugins
#

# Load plugins managed by antibody
source ~/.zsh_plugins.sh

# zsh-history-substring-search

# zoxide (https://github.com/ajeetdsouza/zoxide)
eval "$(zoxide init zsh)"


#
# History Options
#

HISTSIZE="10000"
SAVEHIST="10000"
HISTFILE="$HOME/.zsh_history"

setopt hist_fcntl_lock
setopt hist_ignore_dups
setopt hist_ignore_space
setopt share_history
setopt extended_history


#
# Key Bindings
#

# Jump words with option+left / option+right
bindkey '\e[1;3D' backward-word
bindkey '\e[1;3C' forward-word

# Scroll history using up and down arrow keys
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down

#
# Environment Variables
#

export GOPATH='/Users/danieltrautmann/Development/Go'
export LANG=en_US.UTF-8
export EDITOR=vim
export SSH_AUTH_SOCK=$($(which gpgconf) --list-dirs agent-ssh-socket)


# PATH
export PATH=$HOME/.cargo/bin:$PATH
export PATH=$HOME/.local/bin:$PATH


#
# Aliases
#

alias cdr='cd $(git rev-parse --show-toplevel)'
alias cm='chezmoi'
alias d='docker'
alias dc='docker compose'
alias diff='git diff --no-index'
alias g='git'
alias k='kubectl'
alias l='ll'
alias la='exa -al'
alias ll='exa -l'
alias ls='exa'
alias md='mkdir'
alias tree='exa --tree'
alias v='nvim'


#
# Prompt
#

setopt prompt_subst

autoload -Uz vcs_info
autoload -Uz add-zsh-hook

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' unstagedstr '!'
zstyle ':vcs_info:*' stagedstr '+'
zstyle ':vcs_info:*' nvcsformats '%~'
zstyle ':vcs_info:git:*' formats '%r/%S @ %b%u%c'
zstyle ':vcs_info:git:*' actionformats '%r/%S @ %b (%a|%m) %u%c'

precmd_vcs_info() {
    vcs_info
}
add-zsh-hook precmd precmd_vcs_info

precmd_set_prompt() {
    local kube_context=$(cat ~/.kube/config | grep "current-context:" | sed "s/current-context: //")

    local section_status="%K{007}%(?.%F{002}.%F{001}) %? %f%k"
	local section_kubectx="%K{006}%F{007} ${kube_context} %f%k"

    local top="${section_status}${section_kubectx} ${vcs_info_msg_0_}"
    local bottom='%# '

    PROMPT="$top"$'\n'"$bottom"
}
add-zsh-hook precmd precmd_set_prompt
